PROJECT := "GitReviewIt.xcodeproj"
SCHEME := "GitReviewIt"
MACOS_PLATFORM := "platform=macOS"

# Default recipe - shows available commands
default:
    @just --list --unsorted

# Build the project for macOS
build: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} build | xcpretty

# Clean and build the project
clean-build: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} clean build | xcpretty

# Run tests
test:
    xcodebuild -project {{ PROJECT }} -scheme {{ SCHEME }} \
        -destination {{ MACOS_PLATFORM }} test

# Clean the build artifacts
clean: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} clean | xcpretty

# Open the project in Xcode
open:
    open {{ PROJECT }}

# Lint the code
lint:
    swiftlint lint --config ../.swiftlint.yml --no-cache

# Bootstrap app for development
bootstrap: install-tools

[private]
install-tools: install-xcpretty

[private]
install-xcpretty:
    #!/bin/zsh

    if ! command -v xcpretty &> /dev/null
    then
        echo "ðŸ“¦ xcpretty is not installed. Installing via Gem..."
        gem install create-dmg || exit 1
    fi
